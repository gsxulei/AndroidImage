apply plugin: 'com.android.application'

apply plugin: 'AndResGuard'

buildscript
{
	repositories
	{
		maven { url "https://maven.aliyun.com/repository/public/" }
		//mavenCentral()
		//jcenter()
		google()
	}
	dependencies
	{
		classpath 'com.tencent.mm:AndResGuard-gradle-plugin:1.2.21'
	}
}

andResGuard
{
	// mappingFile=file("./resource_mapping.txt")
	mappingFile=null
	use7zip=true
	useSign=true
	// 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
	keepRoot=false
	whiteList=
	[
		// for your icon
		"R.drawable.icon",
		// for fabric
		"R.string.com.crashlytics.*",
		// for google-services
		"R.string.google_app_id",
		"R.string.gcm_defaultSenderId",
		"R.string.default_web_client_id",
		"R.string.ga_trackingId",
		"R.string.firebase_database_url",
		"R.string.google_api_key",
		"R.string.google_crash_reporting_api_key"
	]

	compressFilePattern=
	[
		"*.png",
		"*.jpg",
		"*.jpeg",
		"*.gif",
	]

	sevenzip
	{
		//artifact = 'com.tencent.mm:SevenZip:1.2.15'
		path="D:\\ProgramFiles\\7z\\7z1805-extra\\x64\\7za"
	}

	/**
	 * 可选： 如果不设置则会默认覆盖assemble输出的apk
	 **/
	finalApkBackupPath="${project.rootDir}/app/build/outputs/apk/gsxulei/release/final.apk"

	/**
	 * 可选: 指定v1签名时生成jar文件的摘要算法
	 * 默认值为“SHA-1”
	 **/
	// digestalg = "SHA-256"
}

android
{
	//compileSdkVersion rootProject.ext.compileSdkVersion
	compileSdkVersion var.compileSdkVersion
	defaultConfig
	{
		applicationId "com.x62.image"
		minSdkVersion var.minSdkVersion
		targetSdkVersion var.targetSdkVersion
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		//只保留中文资源
		resConfig("zh-rCN")

		multiDexEnabled true


		defaultConfig
		{
			//分包1,指定某个类在maindex，例如LeakCanary启动的时候会初始化一个ContentProvider
			multiDexEnabled true
			multiDexKeepProguard file('multiDexKeep.pro') // 打包到main dex的这些类的混淆规制，没特殊需求就给个空文件
			multiDexKeepFile file('maindexlist.txt') // keep specific classes
		}
	}

	//是否检查图片格式(png、点9)正确
	aaptOptions
	{
		cruncherEnabled=false
		useNewCruncher=false
	}

	//证书配置 keytool -printcert -file CERT.RSA
	signingConfigs
	{
		debug
		{
			storeFile file("../gsxulei.jks")
			storePassword 'androidcode'
			keyAlias 'gsxulei'
			keyPassword 'androidcode'
		}

		release
		{
			storeFile file("../gsxulei.jks")
			storePassword 'androidcode'
			keyAlias 'gsxulei'
			keyPassword 'androidcode'
		}
	}

	buildTypes
	{
		debug
		{
			//在BuildConfig中增加一些常量
			buildConfigField "String", "BASE_URL", "\"http://www.jianshu.com\""
		}

		release
		{
			minifyEnabled true
			//无用资源生成占位
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	flavorDimensions "product"
	productFlavors
	{
		gsxulei
		{
			dimension "product"
			applicationId "com.x62.gsxulei.demo"
		}

		gallery
		{
			dimension "product"
			applicationId "com.x62.gsxulei.gallery.demo"
		}
	}

	//每个模块按照此方式添加独立的资源文件
	sourceSets.main
	{
		res.srcDirs+='src/main/res'
		res.srcDirs+='src/main/res-commons'
	}

	//排除不想添加到apk中的文件
	packagingOptions
	{
		//exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
		exclude 'publicsuffixes.gz'
		//exclude 'META-INF/proguard/proguard.pro'
		//exclude 'META-INF/proguard/okhttp3.pro'
		//exclude 'META-INF/com.android.support_exifinterface.version'
		exclude 'android**'
		exclude 'META-INF**'
	}

	//修改生成的apk文件名
	applicationVariants.all
	{
		variant->variant.outputs.all
		{
			if("release"==variant.buildType.name)
			{
				outputFileName="app_${defaultConfig.versionName}_"
				outputFileName+=variant.productFlavors[0].name
				outputFileName+="."+releaseTime()+".apk"
			}
		}
	}

	//编译之前操作
	applicationVariants.all
	{
		variant->variant.assemble.doFirst
		{
			println "编译之前操作"
		}
	}

	//编译完成后执行安装启动脚本
	applicationVariants.all
	{
		variant->variant.assemble.doLast
		{
			exec
			{
				workingDir './install'
				//commandLine=['cmd','/c','install.bat',variant.buildType.name,variant.outputs[0].outputFile]
				def buildType=variant.buildType.name
				def apkName=variant.outputs[0].outputFile
				def applicationId=variant.productFlavors[0].applicationId
				commandLine=['cmd','/c','install.bat',buildType,apkName,applicationId]
			}
		}
	}

//	sourceSets
//	{
//		main
//		{
//			jniLibs.srcDirs=['libs']
//		}
//	}

	//设置JDK1.8支持Lambda表达式
	compileOptions
	{
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

/**
 * 获取当前时间
 *
 * @return 当前时间
 */
def static releaseTime()
{
	//,TimeZone.getTimeZone("UTC")
	return new Date().format("yyyyMMdd.HHmmss")
}

dependencies
{
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation var.appcompat
	implementation var.recyclerview
	implementation var.design
	implementation var.supportV13

	implementation var.glide
	implementation var.gson
	implementation var.SubsamplingScaleImageView

	implementation var.retrofit
	implementation var.ConverterGson
	implementation var.OkHttpLogging

	compileOnly files("${android.getSdkDirectory().getAbsolutePath()}/platforms/android-23/data/layoutlib.jar")

	implementation 'com.android.support:multidex:1.0.3'

	implementation 'com.github.markzhai:blockcanary-android:1.5.0'

	implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'

	// 启动器
	//implementation 'com.github.zeshaoaaa:LaunchStarter:0.0.1'

	//implementation 'org.greenrobot:eventbus:3.1.1'
	//implementation 'com.android.support.constraint:constraint-layout:1.1.0'
	//testImplementation 'junit:junit:4.12'
	//androidTestImplementation 'com.android.support.test:runner:1.0.1'
	//androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

	//implementation project(':commons')
}